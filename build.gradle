group 'me.gabytm.util'
version '1.0.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()

        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    version = this.version

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc
    }

    javadoc {
        destinationDir = file('../docs/' + project.version + '/' + project.name.replace('actions-', ''))

        options.encoding = 'UTF-8'

        if (options instanceof StandardJavadocDocletOptions) {
            def opt = options as StandardJavadocDocletOptions

            opt.links(
                    'https://docs.oracle.com/javase/8/docs/api/',
                    'https://javadoc.io/doc/org.jetbrains/annotations/20.1.0/'
            )

            if (JavaVersion.current().isJava9Compatible()) {
                opt.addBooleanOption('html5', true)
                opt.addStringOption('-release', '9')
            }

            if (project.name != 'actions-core') {
                //opt.links('https://gabytm.me/docs/actions/' + project.version + '/core/')
            }
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    url = 'https://github.com/iGabyTM/actions'

                    scm {
                        connection = 'scm:git:git://github.com/iGabyTM/actions.git'
                        developerConnection = 'scm:git:ssh://github.com/iGabyTM/actions.git'
                        url = 'https://github.com/iGabyTM/actions'
                    }
                }
            }
        }

        repositories {
            mavenLocal()
        }
    }

}
